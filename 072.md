Sulpiride

medium

# User can brick his wallet by making an incorrect withdrawal request

## Summary
User can brick the wallet by sending 0 to `withdrawalRequest` function, i.e. the user won't be able to make `withdrawAllowance` and `withdrawalRequest` ever again. `0` is a frequently encountered number when interacting with contracts, so there's some probability of such an action happening.

## Vulnerability Detail
In order to make a withdrawal request, a user must call `withdrawalRequest` to request for funds and `withdrawalAllowance` when funds are arrived.\
`withdrawalRequest` allows setting 0 as the request amount and also makes sure that the user doesn't make two requests at the same time by checking if user's `withdrawalRequestPeriod` is 0.
`withdrawAllowance`, on the contrary, requires users's `withdrawalAllowance` to be greater than 0 to reset the value of ``
withdrawalRequestPeriod

## Impact
The user will never be able to call `withdrawAllowance` and `withdrawRequest` again.

## Code Snippet
MainVault.withdrawalRequest:
https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/MainVault.sol#L146-L162
```sol
function withdrawalRequest(
    uint256 _amount
  ) external nonReentrant onlyWhenVaultIsOn returns (uint256 value) {
    UserInfo storage user = userInfo[msg.sender];
    require(user.withdrawalRequestPeriod == 0, "Already a request");

    value = (_amount * exchangeRate) / (10 ** decimals());

    _burn(msg.sender, _amount);

    user.withdrawalAllowance = value;
    user.withdrawalRequestPeriod = rebalancingPeriod;
    totalWithdrawalRequests += value;
}
```

MainVault.withdrawAllowance:
https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/MainVault.sol#L164-L179
```sol
function withdrawAllowance() external nonReentrant onlyWhenIdle returns (uint256 value) {
    UserInfo storage user = userInfo[msg.sender];
    require(user.withdrawalAllowance > 0, allowanceError);
    require(rebalancingPeriod > user.withdrawalRequestPeriod, "Funds not arrived");

    value = user.withdrawalAllowance;
    value = checkForBalance(value);

    reservedFunds -= value;
    delete user.withdrawalAllowance;
    delete user.withdrawalRequestPeriod;

    transferFunds(msg.sender, value);
}
```

**POC (in `test/unit/vault/VaultWithdraw.test.ts`):**
```ts
it('should withdrawRequest 0', async function() {
    const { vault, user } = await setupVault();

    // Period = 1
    await vault.upRebalancingPeriodTEST();

    // User deposits
    await vault.connect(user).deposit(parseUSDC(10_000), user.address); // 10k
    expect(await vault.totalSupply()).to.be.equal(parseUSDC(10_000)); // 10k

    // Withdraws 0
    await expect(() =>
      vault.connect(user).withdrawalRequest(0),
    ).to.changeTokenBalance(vault, user, 0);
    expect(await vault.connect(user).getWithdrawalAllowance()).to.be.equal(parseUSDC(0));

    // Period = 2
    await vault.upRebalancingPeriodTEST();

    // Can't withdraw allowance
    await expect(
      vault.connect(user).withdrawAllowance()
    ).to.be.revertedWith("!Allowance");
    // Can't request withdraw
    await expect(
      vault.connect(user).withdrawalRequest(parseUSDC(1000))
    ).to.be.revertedWith("Already a request");
  })
```


## Tool used

Manual Review

## Recommendation
Consider checking `withdrawalAllowance` instead of `withdrawalRequestPeriod` in `withdrawalRequest` function

```sol
function withdrawalRequest(
    uint256 _amount
  ) external nonReentrant onlyWhenVaultIsOn returns (uint256 value) {
    UserInfo storage user = userInfo[msg.sender];
    require(user.withdrawalAllowance == 0, "Already a request"); // <--- HERE

    value = (_amount * exchangeRate) / (10 ** decimals());

    _burn(msg.sender, _amount);

    user.withdrawalAllowance = value;
    user.withdrawalRequestPeriod = rebalancingPeriod;
    totalWithdrawalRequests += value;
  }
```

Or require that the `_amount > 0`

```sol
...
UserInfo storage user = userInfo[msg.sender];
require(_amount > 0, "!_amount > 0");
require(user.withdrawalRequestPeriod == 0, "Already a request");
...
```