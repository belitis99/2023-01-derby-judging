saidam017

high

# Adding blacklist protocol could break vault rebalance process

## Summary

Adding blacklist protocol inside on of the vault, could break whole vaults rebalance process.

## Vulnerability Detail

When guardian add blacklist protocol via `blacklistProtocol()`, it will prevent protocol to be involved in rebalancing process and prevent adding `deltaAllocations[_protocolNum]` into the blacklisted protocol. 

However, when the game contract call `pushAllocationsToVaults()`, there is no way to control or ensure game players not putting any delta to the blacklisted protocol, causing the call will always revert and rebalance process for this specific vault and chain id will stuck. And further, the next cycle rebalance process for the whole chain in this vault number also can't be initiated.

Scenario :
- Vault exist in 4 chains.
- one of the chain (3rd chain for instance) has 5 protocol, and of the protocol (4th protocol for instance) is blacklisted.
- 4th protocol from the 3rd chain have non zero delta allocations from players.
- rebalance process initiated.
- when entering `pushAllocationsToVaults()`, it will revert because it try to put non zero allocation for blacklisted protocol. 
- rebalance process for this chain id's vault is stuck.
- Eventually, can't call `pushAllocationsToController()` and initiate next cycle rebalance for other chain ids because not all previous rebalance process is finished.

## Impact

Vault containing blacklisted protocol can be stuck, and can't initiate whole rebalance process for the next cycle. and game player can't `rebalanceBasket()` since the stuck vault is still in rebalance process.

## Code Snippet

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Game.sol#L465-L477

`Game` contract  `pushAllocationsToVaults()` will be called and push deltas (which can containing non zero allocation for the blacklisted protocol) to the `xProvider`.
```solidity
  function pushAllocationsToVaults(uint256 _vaultNumber, uint32 _chain) external payable {
    address vault = getVaultAddress(_vaultNumber, _chain);
    require(vault != address(0), "Game: not a valid vaultnumber");
    require(isXChainRebalancing[_vaultNumber][_chain], "Vault is not rebalancing");

    int256[] memory deltas = protocolAllocationsToArray(_vaultNumber, _chain);

    IXProvider(xProvider).pushProtocolAllocationsToVault{value: msg.value}(_chain, vault, deltas);

    emit PushProtocolAllocations(_chain, getVaultAddress(_vaultNumber, _chain), deltas);

    isXChainRebalancing[_vaultNumber][_chain] = false;
  }
```

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/XProvider.sol#L402-L414

xProvider will push the deltas to the vault.
```solidity
function pushProtocolAllocationsToVault(
    uint32 _chainId,
    address _vault,
    int256[] memory _deltas
  ) external payable onlyGame {
    if (_chainId == homeChain) return IVault(_vault).receiveProtocolAllocations(_deltas);
    else {
      bytes4 selector = bytes4(keccak256("receiveProtocolAllocationsToVault(address,int256[])"));
      bytes memory callData = abi.encodeWithSelector(selector, _vault, _deltas);

      xSend(_chainId, callData, 0);
    }
  }
```

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/MainVault.sol#L343-L358

`receiveProtocolAllocations()` will call `receiveProtocolAllocationsInt()` and iterate trough the deltas, if it is non zero, it will try to set it by calling `setDeltaAllocationsInt()`
```solidity
  function receiveProtocolAllocations(int256[] memory _deltas) external onlyXProvider {
    receiveProtocolAllocationsInt(_deltas);
  }

  /// @notice Step 6 end; Game pushes deltaAllocations to vaults
  /// @notice Receives protocol allocation array from the game and settles the allocations
  /// @param _deltas Array with delta allocations where the index matches the protocolId
  function receiveProtocolAllocationsInt(int256[] memory _deltas) internal {
    for (uint i = 0; i < _deltas.length; i++) {
      int256 allocation = _deltas[i];
      if (allocation == 0) continue;
      setDeltaAllocationsInt(i, allocation);
    }

    deltaAllocationsReceived = true;
  }
```

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Vault.sol#L396-L400

But it will revert if the protocol is blacklisted.
```solidity
  function setDeltaAllocationsInt(uint256 _protocolNum, int256 _allocation) internal {
    require(!controller.getProtocolBlacklist(vaultNumber, _protocolNum), "Protocol on blacklist");
    deltaAllocations[_protocolNum] += _allocation;
    deltaAllocatedTokens += _allocation;
  }
```

## Tool used

Manual Review

## Recommendation

Consider adding check inside Game contract and enforce user to remove allocation for the blacklisted protocol.
