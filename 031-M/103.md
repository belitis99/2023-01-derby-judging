rvierdiiev

high

# Game.rebalanceBasket can be called by user, when rewards are not sent yet for the period, in order to avoid slashing dervy token for negative rewards

## Summary
Game.rebalanceBasket can be called by user, when rewards are not sent yet for the period, in order to avoid slashing dervy token for negative rewards
## Vulnerability Detail
Game.rebalanceBasket is entry point for player, to make allocations into the vault.
https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Game.sol#L318-L333
```solidity
  function rebalanceBasket(
    uint256 _basketId,
    int256[][] memory _deltaAllocations
  ) external onlyBasketOwner(_basketId) nonReentrant {
    uint256 vaultNumber = baskets[_basketId].vaultNumber;
    for (uint k = 0; k < chainIds.length; k++) {
      require(!isXChainRebalancing[vaultNumber][chainIds[k]], "Game: vault is xChainRebalancing");
    }


    addToTotalRewards(_basketId);
    int256 totalDelta = settleDeltaAllocations(_basketId, vaultNumber, _deltaAllocations);


    lockOrUnlockTokens(_basketId, totalDelta);
    setBasketTotalAllocatedTokens(_basketId, totalDelta);
    setBasketRebalancingPeriod(_basketId, vaultNumber);
  }
```
As you can see, you can call this function only when all chains are not rebalancing: `!isXChainRebalancing[vaultNumber][chainIds[k]]`.

Now let's check, when `isXChainRebalancing` variable is changed. It is set to `true`, when [allocations are sent to xController](https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Game.sol#L435). So that means, that once it's done, then players can't call `rebalanceBasket`, till `isXChainRebalancing` is set to false.
[This happens](https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Game.sol#L476) inside `pushAllocationsToVaults` function. Once call is received by MainVault, there [are no any restrictions](https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/MainVault.sol#L343-L358) when the call can be done. That actually means that any user can call `Game.pushAllocationsToVaults` right after `pushAllocationsToController` call. After that user can call `Game.rebalanceBasket` again without problems.

Now let's check why this can be a problem.
When user provides his allocations, then [rewards are calculated](https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Game.sol#L396-L398) for him for previous period, depending on his previous allocations.
https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Game.sol#L368-L401
```solidity
  function addToTotalRewards(uint256 _basketId) internal onlyBasketOwner(_basketId) {
    if (baskets[_basketId].nrOfAllocatedTokens == 0) return;


    uint256 vaultNum = baskets[_basketId].vaultNumber;
    uint256 currentRebalancingPeriod = vaults[vaultNum].rebalancingPeriod;
    uint256 lastRebalancingPeriod = baskets[_basketId].lastRebalancingPeriod;


    if (currentRebalancingPeriod <= lastRebalancingPeriod) return;


    for (uint k = 0; k < chainIds.length; k++) {
      uint32 chain = chainIds[k];
      uint256 latestProtocol = latestProtocolId[chain];
      for (uint i = 0; i < latestProtocol; i++) {
        int256 allocation = basketAllocationInProtocol(_basketId, chain, i) / 1E18;
        if (allocation == 0) continue;


        int256 lastRebalanceReward = getRewardsPerLockedToken(
          vaultNum,
          chain,
          lastRebalancingPeriod,
          i
        );
        int256 currentReward = getRewardsPerLockedToken(
          vaultNum,
          chain,
          currentRebalancingPeriod,
          i
        );
        baskets[_basketId].totalUnRedeemedRewards +=
          (currentReward - lastRebalanceReward) *
          allocation;
      }
    }
  }
```
User is allowed to calculate rewards, when `currentRebalancingPeriod > lastRebalancingPeriod`. When user calls `rebalanceBasket` function, then `lastRebalancingPeriod` [is set for the basket](https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Game.sol#L230), same as next rebalancing period. Note, that once `pushAllocationsToController` is called, then `vaults[_vaultNumber].rebalancingPeriod` [is increased](https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Game.sol#L442), new period is started.
That means that if user will call `rebalanceBasket` again after `pushAllocationsToController`, then his `lastRebalancingPeriod` will be in future and he will not be able to receive rewards for previous cycle.

Example.
1.In cycle 10 user allocated some tokens to provider A using `rebalanceBasket` function. So his `lastRebalancingPeriod` becomes 11.
2.Then `pushAllocationsToController` is called, which has increased `vaults[_vaultNumber].rebalancingPeriod` to 11.
3.Then `pushAllocationsToVaults` is called which allows user to call `rebalanceBasket` again.
4.User calls `rebalanceBasket` with new allocations and his `lastRebalancingPeriod` is set to 12 now, but rewards were not claimed for 11th cycle.
5.As result user can't receive rewards for cycle 11 as his `lastRebalancingPeriod` is 12 which means that he already claimed it.

This can be considered as 2 problems.
1.User can lose rewards by mistake.
2.User can call this on purpose, when he will likely receive negative rewards, so he can avoid [slashing of derby tokens](https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Game.sol#L296-L311). Using such strategy user can be sure, that when his rewards are bad, he can just skip adding them.
## Impact
User can avoid be slashed when rewards are negative for his basket in the period.
## Code Snippet
Provided above
## Tool used

Manual Review

## Recommendation
You should restrict call of Game.rebalanceBasket, till `Game.settleRewards` is called.