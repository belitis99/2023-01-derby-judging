hyh

medium

# If Connext don't use all allowance and token employs approval race protection the cross chain functionality can become stuck

## Summary

Trying to approve positive amount when allowance is positive already can stuck xTransfer() as not all the tokens allow to do that.

## Vulnerability Detail

Some ERC20 doesn't allow to set non-zero allowance from non-zero one:

https://github.com/d-xo/weird-erc20#approval-race-protections

If `vaultCurrency` is one of the such tokens and there be a situation of Connext not using previous allowance, xTransfer will be reverted until XProvider be upgraded.

As many core tokens are upgradable (USDC, USDT and so forth), such approval protection can not be in place now, but can be introduced in the future (i.e. it is now largely given up as an approach, but there can surface some additional reasons in the future and it can be reintroduced).

## Impact

xTransfer() will become unavailable for the token, i.e. unavailable for the Vaults whose `vaultCurrency` it is, disabling the protocol's x-chain functionality, both new cross-chain investments and withdrawals from other chains, as it can't proceed without fund transfers.

## Code Snippet

xTransfer() approves Connext for non-zero `_amount`:

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/XProvider.sol#L133-L161

```solidity
  function xTransfer(
    address _token,
    uint256 _amount,
    address _recipient,
    uint32 _destinationDomain,
    uint256 _slippage,
    uint256 _relayerFee
  ) internal {
    require(
      IERC20(_token).allowance(msg.sender, address(this)) >= _amount,
      "User must approve amount"
    );

    // User sends funds to this contract
    IERC20(_token).transferFrom(msg.sender, address(this), _amount);

    // This contract approves transfer to Connext
    IERC20(_token).approve(address(connext), _amount);

    IConnext(connext).xcall{value: (msg.value - _relayerFee)}(
      _destinationDomain, // _destination: Domain ID of the destination chain
      _recipient, // _to: address receiving the funds on the destination
      _token, // _asset: address of the token contract
      msg.sender, // _delegate: address that can revert or forceLocal on destination
      _amount, // _amount: amount of tokens to transfer
      _slippage, // _slippage: the maximum amount of slippage the user will accept in BPS (e.g. 30 = 0.3%)
      bytes("") // _callData: empty bytes because we're only sending funds
    );
  }
```

## Tool used

Manual Review

## Recommendation

Consider approving zero first:

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/XProvider.sol#L133-L161

```solidity
  function xTransfer(
    address _token,
    uint256 _amount,
    address _recipient,
    uint32 _destinationDomain,
    uint256 _slippage,
    uint256 _relayerFee
  ) internal {
    ...

    // This contract approves transfer to Connext
+   IERC20(_token).approve(address(connext), 0);
    IERC20(_token).approve(address(connext), _amount);

    IConnext(connext).xcall{value: (msg.value - _relayerFee)}(
      _destinationDomain, // _destination: Domain ID of the destination chain
      _recipient, // _to: address receiving the funds on the destination
      _token, // _asset: address of the token contract
      msg.sender, // _delegate: address that can revert or forceLocal on destination
      _amount, // _amount: amount of tokens to transfer
      _slippage, // _slippage: the maximum amount of slippage the user will accept in BPS (e.g. 30 = 0.3%)
      bytes("") // _callData: empty bytes because we're only sending funds
    );
  }
```