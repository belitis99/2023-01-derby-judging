nobody2018

high

# At step 5 attacker can prevent xChainController from sending funds to vaults

## Summary
When the system is **in step 5 of the rebalancing period**, anyone can push funds to the specified vault on the specified chain by calling the [[XChainController.sendFundsToVault](https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/XChainController.sol#L409-L441)]. Without checking whether it has been sent, the attacker can repeatedly call this function with the same parameters, causing the **vaultStage[_vaultNumber].fundsSent** variable to be reset. Later, the off-chain keeper failed to execute this function because the condition of **onlyWhenFundsReceived** modifier was not met.
## Vulnerability Detail
Let's assume some states of the current system in step 5:

- No.1 usdc-Vault, No.1 chain, No.2 chain and No.3 chain.
- The No. 1 chain needs 10000 usdc.
- The No. 2 chain needs 20000 usdc.
- The No. 3 chain needs 15000 usdc.

The attacker deployed a contract with the following pseudocode:

```solidity
contract Fun {
  ...
  function start(uint256 _vaultNumber) external {
    for(uint256 chainId = 1; chainId <= 3; chainId++)
      XChainController.sendFundsToVault(_vaultNumber, 0, chainId, 0);
  }
  ...
}
```

Let's see what happens in each loop:

1.  When `sendFundsToVault (1,0,1,0)` is executed for the first time, 10000 usdc are successfully sent to chain 1, `vaultStage [1].fundsSent=1`.
2.  When `sendFundsToVault (1,0,1,0)` is executed for the second time, no usdc is sent, `vaultStage[1].fundsSent=2`
3.  When `sendFundsToVault (1,0,1,0)` is executed for the third time, no usdc is sent, `vaultStage[1].fundsSent=3`, and `resetVaultStages(1)` is executed resulting in `vaultStage[1].fundsReceived` being reset to 0.

**XChainController only sent 10000 usdc, and 35000 usdc was left in itself**. Only the vault state of No.1 chain becomes `State.RebalanceVault`, and the vault state of the other two chains is still `State.WaitingForFunds`. **The rebalance state of the system is stuck**.
## Impact
This problem prevents xChainController from rebalancing funds between vaults, resulting in the system not working properly.
## Code Snippet
```solidity
function sendFundsToVault(
    uint256 _vaultNumber,
    uint256 _slippage,
    uint32 _chain,
    uint256 _relayerFee
  ) external payable onlyWhenFundsReceived(_vaultNumber) {
    address vault = getVaultAddress(_vaultNumber, _chain);
    require(vault != address(0), "xChainController: not a valid vaultnumber");
    if (!getVaultChainIdOff(_vaultNumber, _chain)) {
      uint256 amountToDeposit = getAmountToDeposit(_vaultNumber, _chain);

      if (amountToDeposit > 0) {
        address underlying = getUnderlyingAddress(_vaultNumber, _chain);

        uint256 balance = IERC20(underlying).balanceOf(address(this));
        if (amountToDeposit > balance) amountToDeposit = balance;

        IERC20(underlying).safeIncreaseAllowance(address(xProvider), amountToDeposit);
        xProvider.xTransferToVaults{value: msg.value}(
          vault,
          _chain,
          amountToDeposit,
          underlying,
          _slippage,
          _relayerFee
        );
        setAmountToDeposit(_vaultNumber, _chain, 0);
        emit SentFundsToVault(vault, _chain, amountToDeposit, underlying);
      }
    }
    vaultStage[_vaultNumber].fundsSent++;	//here
    if (vaultStage[_vaultNumber].fundsSent == chainIds.length) resetVaultStages(_vaultNumber);	//reset
  }
```
## Tool used

Manual Review

## Recommendation
Add a cache mark to each chain of each vault to indicate whether it has been sent. Check cache mark at the beginning of this function and set cache mark at the end of this function.