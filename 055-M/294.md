SPYBOY

high

# pushVaultAmounts() will always revert or will return wrong exchange rate

## Summary
In rebalancing flow `pushVaultAmounts()` plays an important role in XChainController.sol contract. I found that the function `pushVaultAmounts()` will always revert or will return the wrong exchange rate. This vulnerability is dependent on the rebalancing flow. That's why To make it more understood below mentioning the rebalancing flow. **(ignore this flow if you know already how rebalancing flow works)** 

**REBALANCING FLOW:** 

1)execute Game.pushAllocationsToController() which will trigger xProvider.pushAllocations() and this will lead to execute XChainController.receiveAllocationsFromGame(). now allocation has been received in the controller.

2)Now Game should send protocol allocation to Mainvault for that run  Game.pushAllocationsToVaults() which will trigger xProvider. pushProtocolAllocationsToVault() and this will lead to execute MainVault.receiveProtocolAllocations(). now protocol allocation has been received in MainVault.

3)execute MainVault.pushTotalUnderlyingToController() which will trigger xProvider.pushTotalUnderlying() and this will lead to execute XChainController.setTotalUnderlying(). Now underlying of Mainvault has been received in the controller

4)now push amountTosend in the vault using XChainController.pushVaultAmounts() which will trigger xProvider.pushSetXChainAllocation() and this will lead to executing MainVault.setXChainAllocation(). now amountTosend and exchange Rate has been received in the Mainvault. and also it tells the Mainvault whether the controller is going to deposit any amount.

5)If the controller needs to deposit funds into the vault then execute XChainController.sendFundsToVault() which will trigger xProvider.xTransferToVaults() and this will lead to executing MainVault.receiveFunds().

6)execute MainVault.rebalanceXChain() which will trigger xProvider.xTransferToController() and this will lead to execute XChainController.upFundsReceived() . now the required amount has been received in the controller.

7)Now Xchain rebalancing is done now it's time to rebalance Vault. for that run Vault.rebalance() which will rebalance all the protocols and vault.

8)now send rewards to game using MainVault.sendRewardsToGame() which will trigger xProvider.pushRewardsToGame() and this will lead to execute game.settleRewards().  Now Rebalancing has been done.


## Vulnerability Detail
While examining Rebalancing flow I noticed that `vaults[_vaultNumber].totalWithdrawalRequests` in XChainController.sol  is increasing on every Rebalancing and `vaults[_vaultNumber].totalUnderlying` will be set to zero at the initial phase of rebalancing . `pushVaultAmounts()` is using total of previous withdrawal amounts to calculate current exchangerate. Then it should use total of previous underlying value . but when `receiveAllocationsFromGame` is called it sets `vaults[_vaultNumber].totalUnderlying = 0` that's why `vaults[_vaultNumber].totalUnderlying` contains current underlying not total of all previous underlying. when  `setTotalUnderlying()` is called it adds current withdrawal request to totalwithdrawal request. It looks like its adding current underlying to total underlying but total underlying is set to 0 when `receiveAllocationsFromGame()` is called . that's why `vaults[_vaultNumber].totalUnderlying` contain current underlying not total of all previous values  Then on every rebalancing `vaults[_vaultNumber].totalUnderlying` will be assigned a new value and for `vaults[_vaultNumber].totalWithdrawalRequests` addition of new value will be done . at one point `vaults[_vaultNumber].totalUnderlying < vaults[_vaultNumber].totalWithdrawalRequests` In function `pushVaultAmounts()` subtraction of this two values occurs . At that point transaction will revert .  If subtraction happens then it's returning wrong exchangerate or it will always revert .Because `vaults[_vaultNumber].totalWithdrawalRequests`  contains total of all previous withdrawal request and `vaults[_vaultNumber].totalUnderlying` contains current underlying of MainVault.

`pushVaultAmounts()` calculates the exchange rate wrongly that's why pushVaultAmounts() will always revert or will return the wrong exchange rate.  In the below POC,  we will perform rebalancing example.


STEP n: Rebalancing step as mentioned above rebalancing flow.
USDT: $

POC:  (For simplicity let's say Vault is doing rebalancing the first time )
**First Rebalancing**
1) Let's say using STEP 1 in the rebalancing flow game pushes allocation to the controller.  (STEP 1)

2) now the game pushes protocol allocation to MainVault.  (STEP 2)

3) Let's take an example currently there are $2000 as underlying and users have made $1000 of totalWithdrawalRequests and reservedFunds is 0 because Vault is doing rebalancing for the first time. Now MainVault will push underlying to contoller using `MainVault.pushTotalUnderlyingToController()`    (STEP 3)
```solidity
underlying: 2000, totalWithdrawalRequests: 1000, reservedFunds: 0 (Vault is doing rebalancing for the first time)
 ```

```solidity
function pushTotalUnderlyingToController() external payable onlyWhenIdle {     
    require(rebalanceNeeded(), "!rebalance needed");                      

    setTotalUnderlying();
    uint256 underlying = savedTotalUnderlying + getVaultBalance() - reservedFunds;
           //2000            =  1000          +   1000     +    0
    IXProvider(xProvider).pushTotalUnderlying{value: msg.value}(
      vaultNumber,
      homeChain,
      underlying,          //2000
      totalSupply(),
      totalWithdrawalRequests     //1000
    );

    state = State.PushedUnderlying;
    lastTimeStamp = block.timestamp;

    emit PushTotalUnderlying(
      vaultNumber,
      homeChain,
      underlying,
      totalSupply(),
      totalWithdrawalRequests
    );
  }
```
In the controller setTotalUnderlying() will set totalWithdrawalRequests and totalUnderlying .  which internally calls `setTotalUnderlyingInt()`
```solidity
  function setTotalUnderlying(
    uint256 _vaultNumber,
    uint32 _chainId,
    uint256 _underlying,
    uint256 _totalSupply,
    uint256 _withdrawalRequests
  ) external onlyXProvider onlyWhenAllocationsReceived(_vaultNumber) {
    require(getTotalUnderlyingOnChain(_vaultNumber, _chainId) == 0, "TotalUnderlying already set");
    setTotalUnderlyingInt(_vaultNumber, _chainId, _underlying, _totalSupply, _withdrawalRequests);
  }
```
`setTotalUnderlyingInt()` will set totalWithdrawalRequests and totalUnderlying 
```solidity
vaults[_vaultNumber].totalUnderlying = 2000
vaults[_vaultNumber].totalWithdrawalRequests = 1000
```
```solidity
  function setTotalUnderlyingInt(
    uint256 _vaultNumber,
    uint32 _chainId,
    uint256 _underlying,
    uint256 _totalSupply,
    uint256 _withdrawalRequests
  ) internal {
    vaults[_vaultNumber].totalUnderlyingPerChain[_chainId] = _underlying;
    vaults[_vaultNumber].withdrawalRequests[_chainId] = _withdrawalRequests;
    vaults[_vaultNumber].totalSupply += _totalSupply;
    vaults[_vaultNumber].totalUnderlying += _underlying;      //here
    vaults[_vaultNumber].totalWithdrawalRequests += _withdrawalRequests;    //here
    vaultStage[_vaultNumber].underlyingReceived++;
  }
```
4) Next, as mentioned in the rebalancing flow from step 4 first rebalancing will happen normally.
at the end of rebalancing, states of MainVault will be 
```solidity
underlying: 1000, totalWithdrawalRequests: 0, reservedFunds: 1000 
```

**Second Rebalancing**
1) Let's say using STEP 1 in the rebalancing flow game pushes allocation to the controller using `pushAllocationsToController()` function which will execute `receiveAllocationsFromGame()` in XchainControler.  (STEP 1)
You can see `receiveAllocationsFromGame()` internally calls `receiveAllocationsFromGameInt()`
```solidity
  function receiveAllocationsFromGame(
    uint256 _vaultNumber,
    int256[] memory _deltas
  ) external onlyXProvider onlyWhenReady(_vaultNumber) {
    return receiveAllocationsFromGameInt(_vaultNumber, _deltas);   //here
  }
```
Before going further let's recap the current state 
```solidity
vaults[_vaultNumber].totalUnderlying = 2000
vaults[_vaultNumber].totalWithdrawalRequests = 1000
```
Now `receiveAllocationsFromGameInt()` internally calls  `resetVaultUnderlying()`
```solidity
  function receiveAllocationsFromGameInt(uint256 _vaultNumber, int256[] memory _deltas) internal {
    uint256 activeVaults;

    for (uint256 i = 0; i < chainIds.length; i++) {
      uint32 chain = chainIds[i];
      activeVaults += settleCurrentAllocation(_vaultNumber, chain, _deltas[i]);
      resetVaultUnderlyingForChain(_vaultNumber, chain);
    }

    resetVaultUnderlying(_vaultNumber);   //here internal call 
    setActiveVaults(_vaultNumber, activeVaults);
    setAllocationsReceived(_vaultNumber, true);
    setReady(_vaultNumber, false);
  }
```
`resetVaultUnderlying()`  sets vaults[_vaultNumber].totalUnderlying  to 0.
```solidity
  function resetVaultUnderlying(uint256 _vaultNumber) internal {
    vaults[_vaultNumber].totalUnderlying = 0;         // here it sets to 0
    vaultStage[_vaultNumber].underlyingReceived = 0;
    vaults[_vaultNumber].totalSupply = 0;
  }
```
now the current state is 
```solidity
vaults[_vaultNumber].totalUnderlying = 0
vaults[_vaultNumber].totalWithdrawalRequests = 1000
```
2) now the game pushes protocol allocation to MainVault. (STEP 2)

3) for the second rebalancing let's say there is an extra deposit of $300 and totalWithdrawalRequests of $500.  and  reservedFunds : 1000. Then states of MainVault will be 
```solidity
underlying: 1300, totalWithdrawalRequests: 500, reservedFunds: 1000 
```
Now MainVault will push the underlying to the controller using MainVault.pushTotalUnderlyingToController() (STEP 3) 
```solidity
function pushTotalUnderlyingToController() external payable onlyWhenIdle {     
    require(rebalanceNeeded(), "!rebalance needed");                            

    setTotalUnderlying();
    uint256 underlying = savedTotalUnderlying + getVaultBalance() - reservedFunds;
        //1300                =   1000+   1300  - 1000
    IXProvider(xProvider).pushTotalUnderlying{value: msg.value}(
      vaultNumber,
      homeChain,
      underlying,                // 1300
      totalSupply(),
      totalWithdrawalRequests     //  500
    );

    state = State.PushedUnderlying;
    lastTimeStamp = block.timestamp;

    emit PushTotalUnderlying(
      vaultNumber,
      homeChain,
      underlying,
      totalSupply(),
      totalWithdrawalRequests
    );
  }
```
In the controller setTotalUnderlying() will set totalWithdrawalRequests and totalUnderlying . which internally calls `setTotalUnderlyingInt()`
```solidity
  function setTotalUnderlying(
    uint256 _vaultNumber,
    uint32 _chainId,
    uint256 _underlying,
    uint256 _totalSupply,
    uint256 _withdrawalRequests
  ) external onlyXProvider onlyWhenAllocationsReceived(_vaultNumber) {
    require(getTotalUnderlyingOnChain(_vaultNumber, _chainId) == 0, "TotalUnderlying already set");
    setTotalUnderlyingInt(_vaultNumber, _chainId, _underlying, _totalSupply, _withdrawalRequests);
  }
```
 `setTotalUnderlyingInt()` will set totalWithdrawalRequests and totalUnderlying

```solidity
vaults[_vaultNumber].totalUnderlying =  0+1300 = 1300     
vaults[_vaultNumber].totalWithdrawalRequests = 1000 + 500 = 1500
```
```solidity
function setTotalUnderlyingInt(
    uint256 _vaultNumber,
    uint32 _chainId,
    uint256 _underlying,
    uint256 _totalSupply,
    uint256 _withdrawalRequests
  ) internal {
    vaults[_vaultNumber].totalUnderlyingPerChain[_chainId] = _underlying;
    vaults[_vaultNumber].withdrawalRequests[_chainId] = _withdrawalRequests;
    vaults[_vaultNumber].totalSupply += _totalSupply;
    vaults[_vaultNumber].totalUnderlying += _underlying;                               //here
    vaults[_vaultNumber].totalWithdrawalRequests += _withdrawalRequests;           //here
    vaultStage[_vaultNumber].underlyingReceived++;
  }
```

4) To push amountTosend and exchangerate to MainVault we should execute  `pushVaultAmounts()`  .  In this function you can see that it sets `totalWithdrawalRequests` by executing `getTotalWithdrawalRequests()`  returns  `vaults[_vaultNumber].totalWithdrawalRequests`. In our case `totalWithdrawalRequests` will be `totalWithdrawalRequests = 1500`,   also notice totalUnderlying is calculated by `uint256 totalUnderlying = getTotalUnderlyingVault(_vaultNumber) - totalWithdrawalRequests;`  `getTotalUnderlyingVault()` will return `vaults[_vaultNumber].totalUnderlying` but in our case `vaults[_vaultNumber].totalUnderlying = 1300`. now the subtraction will be `1300-1500` . now this will revert because big number is being subtracted from small number .

```solidity
  function pushVaultAmounts(
    uint256 _vaultNumber,
    uint16 _chain
  ) external payable onlyWhenUnderlyingsReceived(_vaultNumber) {  
    address vault = getVaultAddress(_vaultNumber, _chain);
    require(vault != address(0), "xChainController: not a valid vaultnumber");
    int256 totalAllocation = getCurrentTotalAllocation(_vaultNumber);
    uint256 totalWithdrawalRequests = getTotalWithdrawalRequests(_vaultNumber);               //here
    uint256 totalUnderlying = getTotalUnderlyingVault(_vaultNumber) - totalWithdrawalRequests;    //will revert here
    uint256 totalSupply = getTotalSupply(_vaultNumber);

    uint256 decimals = xProvider.getDecimals(vault);
    uint256 newExchangeRate = (totalUnderlying * (10 ** decimals)) / totalSupply;

    if (!getVaultChainIdOff(_vaultNumber, _chain)) {
      int256 amountToChain = calcAmountToChain(
        _vaultNumber,
        _chain,
        totalUnderlying,
        totalAllocation
      );
      (int256 amountToDeposit, uint256 amountToWithdraw) = calcDepositWithdraw(
        _vaultNumber,
        _chain,
        amountToChain
      );

      sendXChainAmount(_vaultNumber, _chain, amountToDeposit, amountToWithdraw, newExchangeRate);
    }
  }
```
```solidity
  function getTotalWithdrawalRequests(uint256 _vaultNumber) internal view returns (uint256) {
    return vaults[_vaultNumber].totalWithdrawalRequests;
  }
```
```solidity
  function getTotalUnderlyingVault(
    uint256 _vaultNumber
  ) internal view onlyWhenUnderlyingsReceived(_vaultNumber) returns (uint256) {
    return vaults[_vaultNumber].totalUnderlying;
  }
```



## Impact
At one point `XChainController.pushVaultAmounts()` will start reverting . If subtraction between `vaults[_vaultNumber].totalUnderlying ` and  `vaults[_vaultNumber].totalWithdrawalRequests` is possible then its returning wrong exchange rate.  Because `vaults[_vaultNumber].totalWithdrawalRequests`  contains total of all previous withdrawal request and `vaults[_vaultNumber].totalUnderlying` contains current underlying of MainVault. THis vulnerability can disturb rebalancing flow . user or dao can loss there funds when its returns wrong exchange rate

## Code Snippet
receiveAllocationsFromGame(): https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/XChainController.sol#L193-L198
receiveAllocationsFromGameInt() :  https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/XChainController.sol#L203-L216 .
resetVaultUnderlying() : https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/XChainController.sol#L186-L188
pushTotalUnderlyingToController() : https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/MainVault.sol#L249-L273
setTotalUnderlying() : https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/XChainController.sol#L258-L267
setTotalUnderlyingInt() : https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/XChainController.sol#L276-L289  
pushVaultAmounts() : https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/XChainController.sol#L295-L324


## Tool used

Manual Review

## Recommendation
The main cause of this vulnerability is It's using all the previous withdrawals and current underlying to calculate the exchange rate.  It should use total previous withdrawals and total previous underlying . At the initial phase of rebalancing when `receiveAllocationsFromGame()` is called it sets totalundelying is equal to 0 . but while setting underlying value when 
`setTotalUnderlying()` is called it tryes to add  underlying to total underlying but total underlying is set to 0 when `receiveAllocationsFromGame()` is called. Thats why it uses current underlying of vault not total underlying during calculating exchange rate . to fix this issue it should not set totalunderlying = 0 when `receiveAllocationsFromGame()`
```solidity
  function resetVaultUnderlying(uint256 _vaultNumber) internal {
    //vaults[_vaultNumber].totalUnderlying = 0;    Remove this line
    vaultStage[_vaultNumber].underlyingReceived = 0;
    vaults[_vaultNumber].totalSupply = 0;
  }

```
