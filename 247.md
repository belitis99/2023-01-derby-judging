hyh

medium

# Rebalancing can become unavailable when Aave pool has liquidity shortage

## Summary

There is no control for liquidity squeeze that is typical for lending markets and can routinely happen in Aave pools.

Withdrawals exceeding available funds will be reverted, which will block rebalancing that now tries to withdraw what is needed based solely on the AToken balance and fails if this call reverts.

## Vulnerability Detail

A part of lending pool funds is routinely temporary locked, being lent out. Withdrawal requests that exceed the amount of free funds available will be failed.

For example, as of time of this writing USDT Aave pool on mainnet have `534.89m USDT` deposited and only `3.58m USDT`, or about `0.7%`, available for withdrawals.

https://app.aave.com/reserve-overview/?underlyingAsset=0xdac17f958d2ee523a2206206994597c13d831ec7&marketName=proto_mainnet

In `1.76m LUSD` pool only `30k LUSD` is available:

https://app.aave.com/reserve-overview/?underlyingAsset=0x5f98805a4e8be255a32880fdec7f6728c6568ba0&marketName=proto_mainnet

I.e. even for big pools there is no guarantees that given amount can be withdrawn.

## Impact

Rebalancing will be frozen until Aave pool liquidity returns. Corresponding Aave pool cannot be manually removed from Vault allocation to remedy this.

## Code Snippet

Aave withdraw() fails when `IALendingPool(IAToken(_aToken).POOL()).withdraw(..., _amount, ...)` reverts:

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Providers/AaveProvider.sol#L43-L74

```solidity
  /// @notice Withdraw the underlying asset from Aave
  /// @dev Pulls cTokens from Vault, redeem them from Aave, send underlying back.
  /// @param _amount Amount to withdraw
  /// @param _uToken Address of underlying Token eg USDC
  /// @param _aToken Address of protocol LP Token eg aUSDC
  /// @return Underlying tokens received and sent to vault e.g USDC
  function withdraw(
    uint256 _amount,
    address _aToken,
    address _uToken
  ) external override returns (uint256) {
    uint256 balanceBefore = IERC20(_uToken).balanceOf(msg.sender);

    require(
      IAToken(_aToken).transferFrom(msg.sender, address(this), _amount) == true,
      "Error: transferFrom"
    );
>>  uint256 uTokensReceived = IALendingPool(IAToken(_aToken).POOL()).withdraw(
      IAToken(_aToken).UNDERLYING_ASSET_ADDRESS(),
      _amount,
      msg.sender
    );

    uint256 balanceAfter = IERC20(_uToken).balanceOf(msg.sender);

    require(
      (balanceAfter - balanceBefore - uTokensReceived) == 0,
      "Error Withdraw: under/overflow"
    );

    return uTokensReceived;
  }
```

The `_amount` is determined solely based on what is needed and the current protocol's AToken balance:

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Vault.sol#L303-L326

```solidity
  /// @notice Withdraw amount from underlying protocol
  /// @dev shares = amount / PricePerShare
  /// @param _protocolNum Protocol number linked to an underlying protocol e.g compound_usdc_01
  /// @param _amount in VaultCurrency to withdraw
  function withdrawFromProtocol(uint256 _protocolNum, uint256 _amount) internal {
    if (_amount <= 0) return;
    IController.ProtocolInfoS memory protocol = controller.getProtocolInfo(
      vaultNumber,
      _protocolNum
    );

    _amount = (_amount * protocol.uScale) / uScale;
    uint256 shares = IProvider(protocol.provider).calcShares(_amount, protocol.LPToken);
    uint256 balance = IProvider(protocol.provider).balance(address(this), protocol.LPToken);

    if (shares == 0) return;
>>  if (balance < shares) shares = balance;

    IERC20(protocol.LPToken).safeIncreaseAllowance(protocol.provider, shares);
    uint256 amountReceived = IProvider(protocol.provider).withdraw(
      shares,
      protocol.LPToken,
      protocol.underlying
    );
```

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Providers/AaveProvider.sol#L102-L105

```solidity
  function balance(address _address, address _aToken) public view override returns (uint256) {
    uint256 _balanceShares = IAToken(_aToken).balanceOf(_address);
    return _balanceShares;
  }
```

Aave v2 revert withdraw() calls when the amount fits in user balance, but exceeds the liquidity available:

https://github.com/aave/protocol-v2/blob/6f57232358af0fd41d9dcf9309d7a8c0b9aa3912/contracts/protocol/lendingpool/DefaultReserveInterestRateStrategy.sol#L132

```solidity
availableLiquidity = availableLiquidity.add(liquidityAdded).sub(liquidityTaken);
```

https://github.com/aave/protocol-v2/blob/baeb455fad42d3160d571bd8d3a795948b72dd85/contracts/protocol/libraries/logic/ReserveLogic.sol#L233-L235

```solidity
    require(vars.newLiquidityRate <= type(uint128).max, Errors.RL_LIQUIDITY_RATE_OVERFLOW);
    require(vars.newStableRate <= type(uint128).max, Errors.RL_STABLE_BORROW_RATE_OVERFLOW);
    require(vars.newVariableRate <= type(uint128).max, Errors.RL_VARIABLE_BORROW_RATE_OVERFLOW);
```

If Aave pool is a part of allocation, its withdraw() is a part of main rebalancing cycle, which will be blocked while it is reverting:

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Vault.sol#L111-L123

```solidity
  function pullFunds(uint256 _value) internal {
    uint256 latestID = controller.latestProtocolId(vaultNumber);
    for (uint i = 0; i < latestID; i++) {
      if (currentAllocations[i] == 0) continue;

      uint256 shortage = _value - vaultCurrency.balanceOf(address(this));
      uint256 balanceProtocol = balanceUnderlying(i);

      uint256 amountToWithdraw = shortage > balanceProtocol ? balanceProtocol : shortage;
      savedTotalUnderlying -= amountToWithdraw;

      if (amountToWithdraw < minimumPull) break;
>>    withdrawFromProtocol(i, amountToWithdraw);
```

Protocol temporary removal will not be possible either as it tries to withdraw current balance, which will revert:

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Vault.sol#L475-L483

```solidity
  /// @notice The DAO should be able to blacklist protocols, the funds should be sent to the vault.
  /// @param _protocolNum Protocol number linked to an underlying vault e.g compound_usdc_01
  function blacklistProtocol(uint256 _protocolNum) external onlyGuardian {
    uint256 balanceProtocol = balanceUnderlying(_protocolNum);
    currentAllocations[_protocolNum] = 0;
    controller.setProtocolBlacklist(vaultNumber, _protocolNum);
    savedTotalUnderlying -= balanceProtocol;
    withdrawFromProtocol(_protocolNum, balanceProtocol);
  }
```

## Tool used

Manual Review

## Recommendation

Consider using try-catch approach for Aave withdrawal, for example:

```solidity
  try IALendingPool(IAToken(_aToken).POOL()).withdraw(
      IAToken(_aToken).UNDERLYING_ASSET_ADDRESS(),
      _amount,
      msg.sender
    ) {
      uint256 balanceAfter = IERC20(_uToken).balanceOf(msg.sender);

      require(
        (balanceAfter - balanceBefore - _amount) == 0,
        "Error Withdraw: under/overflow"
      );

      return _amount;
  } catch {
      return 0;
  }
```