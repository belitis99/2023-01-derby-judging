hyh

high

# Minimum stablecoin swap output is controlled incorrectly in swapStableCoins and Vault rebalancing can be blocked

## Summary

Swap's swapStableCoins() estimate `amountOutMin` as the amount of input token less fees, without performing price adjustments.

As stablecoin prices can diverge, even massively, it isn't appropriate to assume price equality and use `tokenIn` amount to control `tokenOut` minimum.

## Vulnerability Detail

As a corner case example suppose `_swap.tokenIn` is USDC, while `_swap.tokenOut` is USDT, Curve pool fee is `0.04%`. Suppose USDT is went through a major regulatory scrutiny and it is now priced at `0.98 USD`, USDC at `1.02 USDC` (it's used as safe heaven and has a premium), but this is a locally stable situation and the major USDT Vaults now offer quite attractive returns, so they were whitelisted.

swapStableCoins() being called with `10^12` amount, which is a request to swap `10^6 USDC`, will set `amountOutMin = 10**12 * (10000 - 4) / 10000 = 999600 * 10**6 = 999600 USDT`, which allows for 4% effective slippage, i.e. as this is barely acceptable for the stablecoin case, the slippage control is effectively disabled.

On the other hand, when it is USDT being swapped for USDC, `amountOutMin` will be `999600 USDC`, which will not be achievable given market USDT/USDC rate, so swapping be effectively blocked along with the calling depositInProtocol() and withdrawFromProtocol(), i.e. Vault rebalancing will be frozen.

## Impact

Deposit and withdrawal from a protocol is a base functionality of a Vault and if either of them is blocked, the whole Vault rebalancing is blocked too.

This way it is either lack of slippage control or full rebalancing block as the net impact depending on current state of the market. Notice that it's not needed to be any substantial divergence on the markets to disable the rebalancing. For example, even if USDT is cheaper than USDC by mere 0.05%, which can happen routinely, the swap and the whole rebalance() call will be reverted as 3Pool will just not be able to provide 1:1 return.

Lack of rebalancing possibility will alter the effective distribution vs desired and shift actual results from expected, leading to losses for protocol depositors.

## Code Snippet

swapStableCoins() calculates `amountOutMin` as input token amount less fees:

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/libraries/Swap.sol#L31-L58

```solidity
  /// @notice Swap stable coins on Curve
  /// @param _swap Number of tokens to sell, token to sell, token to receive
  /// @param _tokenInUScale Scale of tokenIn e.g 1E6
  /// @param _tokenOutUScale Scale of tokenOut e.g 1E6
  /// @param _curve Curve pool index number of TokenIn address, tokenOut address, pool address and pool fee
  function swapStableCoins(
    SwapInOut memory _swap,
    uint256 _tokenInUScale,
    uint256 _tokenOutUScale,
    IController.CurveParams memory _curve
  ) public returns (uint256) {
    uint256 amountOutMin = (((_swap.amount * (10000 - _curve.poolFee)) / 10000) * _tokenOutUScale) /
      _tokenInUScale;
    IERC20(_swap.tokenIn).safeIncreaseAllowance(_curve.pool, _swap.amount);

    uint256 balanceBefore = IERC20(_swap.tokenOut).balanceOf(address(this));

    IStableSwap3Pool(_curve.pool).exchange(
      _curve.indexTokenIn,
      _curve.indexTokenOut,
      _swap.amount,
      amountOutMin
    );

    uint256 balanceAfter = IERC20(_swap.tokenOut).balanceOf(address(this));

    return balanceAfter - balanceBefore;
  }
```

StableSwap3Pool's exchange requires `amountOutMin` to be met, reverting the call otherwise:

https://github.com/curvefi/curve-contract/blob/master/contracts/pools/3pool/StableSwap3Pool.vy#L470

```vyper
  assert dy >= min_dy, "Exchange resulted in fewer coins than expected"
```

If either depositInProtocol() or withdrawFromProtocol() is unavailable then rebalance() is blocked:

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Vault.sol#L269-L276

```solidity
  function executeDeposits(uint256[] memory protocolToDeposit) internal {
    uint256 latestID = controller.latestProtocolId(vaultNumber);
    for (uint i = 0; i < latestID; i++) {
      uint256 amount = protocolToDeposit[i];
      if (amount == 0) continue;
      depositInProtocol(i, amount);
    }
  }
```

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Vault.sol#L135-L154

```solidity
  function rebalance() external nonReentrant {
    require(state == State.RebalanceVault, stateError);
    require(deltaAllocationsReceived, "!Delta allocations");

    rebalancingPeriod++;

    claimTokens();
    settleDeltaAllocation();

    uint256 underlyingIncBalance = calcUnderlyingIncBalance();
    uint256[] memory protocolToDeposit = rebalanceCheckProtocols(underlyingIncBalance);

    executeDeposits(protocolToDeposit);
    setTotalUnderlying();

    if (reservedFunds > vaultCurrency.balanceOf(address(this))) pullFunds(reservedFunds);

    state = State.SendRewardsPerToken;
    deltaAllocationsReceived = false;
  }
```

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Vault.sol#L111-L123

```solidity
  function pullFunds(uint256 _value) internal {
    uint256 latestID = controller.latestProtocolId(vaultNumber);
    for (uint i = 0; i < latestID; i++) {
      if (currentAllocations[i] == 0) continue;

      uint256 shortage = _value - vaultCurrency.balanceOf(address(this));
      uint256 balanceProtocol = balanceUnderlying(i);

      uint256 amountToWithdraw = shortage > balanceProtocol ? balanceProtocol : shortage;
      savedTotalUnderlying -= amountToWithdraw;

      if (amountToWithdraw < minimumPull) break;
      withdrawFromProtocol(i, amountToWithdraw);
```

## Tool used

Manual Review

## Recommendation

One option is to use operator provided minimum accepted stablecoin bid/ask cross-rates so it can be tuned to the current market conditions on each rebalance. This will also provide control for sandwich attacks.

Alternatively, Uniswap reading can be used to gauge current rate:

```solidity
// IController.UniswapParams _uniswap = controller.getUniswapParams(); // pass as an additional argument for swapStableCoins()
uint256 amountOutMin = Swap.amountOutMultiSwap(
    Swap.SwapInOut(_swap.amount, _curve.indexTokenIn, _curve.indexTokenOut),
    _uniswap.quoter,
    0
);
amountOutMin = amountOutMin * (10000 - _curve.poolFee - allowedSlippageBP) / 10000;
``` 

`allowedSlippageBP` is Vault's allowed slippage parameter.