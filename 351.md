bin2chen

medium

# pullFunds() break should not be used

## Summary
Incorrectly jumped out of the loop, resulting in the amount less withdrawal from the protocol
## Vulnerability Detail
pullFunds() loops through all the protocols and tries to get enough `_value`
The code is as follows:
```solidity
  function pullFunds(uint256 _value) internal {
    uint256 latestID = controller.latestProtocolId(vaultNumber);
    for (uint i = 0; i < latestID; i++) {
      if (currentAllocations[i] == 0) continue;

      uint256 shortage = _value - vaultCurrency.balanceOf(address(this));
      uint256 balanceProtocol = balanceUnderlying(i);

      uint256 amountToWithdraw = shortage > balanceProtocol ? balanceProtocol : shortage;
      savedTotalUnderlying -= amountToWithdraw;

      if (amountToWithdraw < minimumPull) break; //<------break? may be have other protocol need to withdraw
      withdrawFromProtocol(i, amountToWithdraw);

      if (_value <= vaultCurrency.balanceOf(address(this))) break;
    }
  }
```
The above code shows that if `amountToWithdraw < minimumPull` then the whole loop is jumped out
but the `amountToWithdraw` maybe is the current protocol balance `balanceProtocol`
this skips the all next protocol, and there may still be enough amount to withdraw
It should just skip this protocol (continue)
All calls to pullFunds() will take less amount, such as rebalance(), rebalanceXChain()

## Impact

 rebalance(), rebalanceXChain() maybe take less funds

## Code Snippet
https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Vault.sol#L122
## Tool used

Manual Review

## Recommendation
```solidity
  function pullFunds(uint256 _value) internal {
    uint256 latestID = controller.latestProtocolId(vaultNumber);
    for (uint i = 0; i < latestID; i++) {
      if (currentAllocations[i] == 0) continue;

      uint256 shortage = _value - vaultCurrency.balanceOf(address(this));
      uint256 balanceProtocol = balanceUnderlying(i);

      uint256 amountToWithdraw = shortage > balanceProtocol ? balanceProtocol : shortage;
      savedTotalUnderlying -= amountToWithdraw;


-     if (amountToWithdraw < minimumPull) break;
+     if (amountToWithdraw < minimumPull) continue;
      withdrawFromProtocol(i, amountToWithdraw);

      if (_value <= vaultCurrency.balanceOf(address(this))) break;
    }
  }
```