hyh

medium

# Vault's marginScale can be set negative, which will break protocol logic

## Summary

Setting `marginScale` to a negative value is allowed, but this will disrupt the whole protocol, blocking rebalancing and withdrawals.

## Vulnerability Detail

`marginScale` is a `int256` which reset function has no value controls, so it can be set to negative value. On rebalancing there is a `uint(marginScale)` conversion, which will yield corrupted values in this case, which will revert the following calculations.

If that is done by Guardian by an operational mistake or with a malicious intent, the protocol core rebalancing functionality will be frozen as such calls will be reverted.

## Impact

Besides rebalancing itself all the deposit withdrawals become frozen as funds are typically almost fully invested and are freed for the requested withdrawals as a result of rebalancing.

## Code Snippet

`marginScale` needs to be positive in order for protocol deposit / withdraw logic to function properly:

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Vault.sol#L178-L200

```solidity
  function rebalanceCheckProtocols(
    uint256 _newTotalUnderlying
  ) internal returns (uint256[] memory) {
    uint256[] memory protocolToDeposit = new uint[](controller.latestProtocolId(vaultNumber));
    uint256 latestID = controller.latestProtocolId(vaultNumber);
    for (uint i = 0; i < latestID; i++) {
      ...

      int256 amountToProtocol = calcAmountToProtocol(_newTotalUnderlying, i);
      uint256 currentBalance = balanceUnderlying(i);

      int256 amountToDeposit = amountToProtocol - int(currentBalance);
      uint256 amountToWithdraw = amountToDeposit < 0 ? currentBalance - uint(amountToProtocol) : 0;

      if (amountToDeposit > marginScale) protocolToDeposit[i] = uint256(amountToDeposit);
      if (amountToWithdraw > uint(marginScale) || currentAllocations[i] == 0)
        withdrawFromProtocol(i, amountToWithdraw);
    }
```

As otherwise `uint256(amountToDeposit)` and `uint(marginScale)` conversions will corrupt the corresponding values:

https://docs.soliditylang.org/en/latest/types.html#explicit-conversions

However, `marginScale` can be set to any negative value:

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Vault.sol#L485-L491

```solidity
  /// @notice Set the marginScale, the threshold used for deposits and withdrawals.
  /// @notice If the threshold is not met the deposit/ withdrawal is not executed.
  /// @dev Take into account the uScale (scale of the underlying).
  /// @param _marginScale Value at which to set the marginScale.
  function setMarginScale(int256 _marginScale) external onlyGuardian {
    marginScale = _marginScale;
  }
```

As rebalanceCheckProtocols() is a base deposit/withdraw logic piece of vault rebalance():

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Vault.sol#L129-L154

```solidity
  /// @notice Step 7 trigger, end; Vaults rebalance
  /// @notice Rebalances i.e deposit or withdraw from all underlying protocols
  /// @dev amountToProtocol = totalAmount * currentAllocation / totalAllocatedTokens
  /// @dev amountToDeposit = amountToProtocol - currentBalanceProtocol
  /// @dev if amountToDeposit < 0 => withdraw
  /// @dev Execute all withdrawals before deposits
  function rebalance() external nonReentrant {
    require(state == State.RebalanceVault, stateError);
    require(deltaAllocationsReceived, "!Delta allocations");

    rebalancingPeriod++;

    claimTokens();
    settleDeltaAllocation();

    uint256 underlyingIncBalance = calcUnderlyingIncBalance();
    uint256[] memory protocolToDeposit = rebalanceCheckProtocols(underlyingIncBalance);

    executeDeposits(protocolToDeposit);
    setTotalUnderlying();

    if (reservedFunds > vaultCurrency.balanceOf(address(this))) pullFunds(reservedFunds);

    state = State.SendRewardsPerToken;
    deltaAllocationsReceived = false;
  }
```

## Tool used

Manual Review

## Recommendation

Consider controlling the new `marginScale` value:

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Vault.sol#L485-L491

```solidity
  /// @notice Set the marginScale, the threshold used for deposits and withdrawals.
  /// @notice If the threshold is not met the deposit/ withdrawal is not executed.
  /// @dev Take into account the uScale (scale of the underlying).
  /// @param _marginScale Value at which to set the marginScale.
  function setMarginScale(int256 _marginScale) external onlyGuardian {
+   require(_marginScale > 0, "Negative marginScale");
    marginScale = _marginScale;
  }
```

Or, as `marginScale` is `the threshold used for deposits and withdrawals`, consider making it `uint256`.