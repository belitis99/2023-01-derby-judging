HonorLt

medium

# Append only problems

## Summary

Some data sets are susceptible to the infamous unbounded loop gas limit issue.

## Vulnerability Detail

DAO can add a new protocol:
```solidity
  function addProtocol(
    string calldata _name,
    uint256 _vaultNumber,
    address _provider,
    address _protocolLPToken,
    address _underlying,
    address _govToken,
    uint256 _uScale
  ) external onlyDao returns (uint256) {
    uint256 protocolNumber = latestProtocolId[_vaultNumber];

    protocolNames[_vaultNumber][protocolNumber] = _name;
    protocolGovToken[_vaultNumber][protocolNumber] = _govToken;
    protocolInfo[_vaultNumber][protocolNumber] = ProtocolInfoS(
      _protocolLPToken,
      _provider,
      _underlying,
      _uScale
    );

    emit SetProtocolNumber(protocolNumber, _protocolLPToken);

    latestProtocolId[_vaultNumber]++;

    return protocolNumber;
  }
```
However, there is no way to remove it (reduce `latestProtocolId`), which can lead to loop DOS issues later. A lot of tokens iterate over all the protocols. For example, `claimTokens` tries to claim rewards from all of them:
```solidity
  function claimTokens() public {
    uint256 latestID = controller.latestProtocolId(vaultNumber);
    for (uint i = 0; i < latestID; i++) {
      if (currentAllocations[i] == 0) continue;
      bool claim = controller.claim(vaultNumber, i);
      if (claim) {
        address govToken = controller.getGovToken(vaultNumber, i);
        uint256 tokenBalance = IERC20(govToken).balanceOf(address(this));
        Swap.swapTokensMulti(
          Swap.SwapInOut(tokenBalance, govToken, address(vaultCurrency)),
          controller.getUniswapParams(),
          false
        );
      }
    }
  }
```

A similar problem is with vaults. It is possible only to add a new vault, but not remove the existing one:
```solidity
  function addVault(address _vault) external onlyDao {
    vaultWhitelist[_vault] = true;
  }
```
However, there are no iterations over the vaults, so won't cause the aforementioned gas limit issue but still, it might be problematic that a compromised vault cannot be suspended from the system.

## Impact

While the possibility is low, technically it is possible to reach the limit when the execution cost grows too large not fitting in one block and thus blocking the transactions.

## Code Snippet

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Controller.sol#L168

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Vault.sol#L405-L406

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Controller.sol#L175-L177

## Tool used

Manual Review

## Recommendation

Introduce a reasonable upper boundary or functions to remove elements.
