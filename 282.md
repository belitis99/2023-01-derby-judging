HonorLt

medium

# Unnecessary allowance when swapping rewards

## Summary

Swapping multi-tokens can become impossible if the allowance (e.g. USDT) is not 0.

## Vulnerability Detail

When withdrawing rewards, if `swapRewards` is turned on, it performs the multi-token swap:
```solidity
  function withdrawRewards() external nonReentrant onlyWhenIdle returns (uint256 value) {
    ...
    if (swapRewards) {
      uint256 tokensReceived = Swap.swapTokensMulti(
        Swap.SwapInOut(value, address(vaultCurrency), derbyToken),
        controller.getUniswapParams(),
        true
      );
      IERC20(derbyToken).safeTransfer(msg.sender, tokensReceived);
    }
   ...
  }
```

Function `swapTokensMulti` first increases the allowance and only performs the swap if it is not a reward swap and the token balance is not sufficient:
```solidity
  function swapTokensMulti(
    SwapInOut memory _swap,
    IController.UniswapParams memory _uniswap,
    bool _rewardSwap
  ) public returns (uint256) {
    IERC20(_swap.tokenIn).safeIncreaseAllowance(_uniswap.router, _swap.amount);

    uint256 amountOutMinimum = IQuoter(_uniswap.quoter).quoteExactInput(
      abi.encodePacked(_swap.tokenIn, _uniswap.poolFee, WETH, _uniswap.poolFee, _swap.tokenOut),
      _swap.amount
    );

    uint256 balanceBefore = IERC20(_swap.tokenOut).balanceOf(address(this));
    if (_rewardSwap && balanceBefore > amountOutMinimum) return amountOutMinimum;

    ISwapRouter.ExactInputParams memory params = ISwapRouter.ExactInputParams({
      path: abi.encodePacked(
        _swap.tokenIn,
        _uniswap.poolFee,
        WETH,
        _uniswap.poolFee,
        _swap.tokenOut
      ),
      recipient: address(this),
      deadline: block.timestamp,
      amountIn: _swap.amount,
      amountOutMinimum: amountOutMinimum
    });
```
Here it first increases the allowance but the actual swap might never execute if this branch is triggered:
```solidity
     if (_rewardSwap && balanceBefore > amountOutMinimum) return amountOutMinimum;
```

When this function is invoked again later, it will fail with tokens that require the allowance to first reset to 0 before approving again. Such a token is USDT which is included in the supported tokens list. As described in Tether code: To change the approve amount you first have to reduce the addresses allowance to 0 calling `approve(spender, 0)`.

Another issue is that this approval remains and can be used by the router on behalf of this contract, but probably this is not very relevant because the router is trusted and set by the DAO.

## Impact

I think it is a Medium severity issue because the DAO can turn off swapRewards, but nevertheless, it can't be fixed and this function will remain blocked forever.

## Code Snippet

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/libraries/Swap.sol#L69-L93

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/MainVault.sol#L220-L225

## Tool used

Manual Review

## Recommendation

Approve only just before the swap, or first reset the approval to 0.
