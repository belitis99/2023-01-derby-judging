hyh

medium

# XProvider performs unsafe transfers of arbitrary tokens

## Summary

XProvider's xTransferToController(), xTransferToVaults(), xTransfer() use unsafe versions of token transfer for an arbitrary token.

## Vulnerability Detail

Unsafe transfer does guarantee the transfer was fully complete only when it is implemented to fail whenever transfers can't be processed fully. Not all ERC20 implementations behave that way, it isn't required.

Due to that while having unsafe transfer for protocol controlled tokens produces no issues, using it for any external controlled token poses a vulnerability as long as this token implementation do not always fail for unsuccessful transfer, returning false instead, or a token can be upgraded to start behaving this way.

For example, as USDC is an upgradable contract, there is no guarantees that the next implementation will be failing instead of only returning bool success value.

## Impact

Permanent user funds freeze takes place when a token fails to transfer the funds, but the protocol deems the operation as successful and updates the internal accounting accordingly.

## Code Snippet

The success of `IERC20(_asset).transferFrom` isn't controlled:

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/XProvider.sol#L314-L335

```solidity
  /// @notice Step 4 push; Push funds from vaults to xChainController
  /// @notice Transfers funds from vault to xController for crosschain rebalance
  /// @param _vaultNumber Address of the Derby Vault on given chainId
  /// @param _amount Number of the vault
  /// @param _asset Address of the token to send e.g USDC
  /// @param _slippage Slippage tollerance for xChain swap, in BPS (i.e. 30 = 0.3%)
  /// @param _relayerFee The fee offered to the relayers
  function xTransferToController(
    uint256 _vaultNumber,
    uint256 _amount,
    address _asset,
    uint256 _slippage,
    uint256 _relayerFee
  ) external payable onlyVaults {
    if (homeChain == xControllerChain) {
      IERC20(_asset).transferFrom(msg.sender, xController, _amount);
      IXChainController(xController).upFundsReceived(_vaultNumber);
    } else {
      xTransfer(_asset, _amount, xController, xControllerChain, _slippage, _relayerFee);
      pushFeedbackToXController(_vaultNumber, _relayerFee);
    }
  }
```

Similarly, `IERC20(_asset).transferFrom` can fail and do not revert:

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/XProvider.sol#L355-L377

```solidity
  /// @notice Step 5 push; Push funds from xChainController to vaults
  /// @notice Transfers funds from xController to vault for crosschain rebalance
  /// @param _chainId Number of chainId
  /// @param _amount Amount to send to vault in vaultcurrency
  /// @param _asset Addres of underlying e.g USDC
  /// @param _slippage Slippage tollerance for xChain swap, in BPS (i.e. 30 = 0.3%)
  /// @param _relayerFee The fee offered to the relayers
  function xTransferToVaults(
    address _vault,
    uint32 _chainId,
    uint256 _amount,
    address _asset,
    uint256 _slippage,
    uint256 _relayerFee
  ) external payable onlyController {
    if (_chainId == homeChain) {
      IVault(_vault).receiveFunds();
      IERC20(_asset).transferFrom(msg.sender, _vault, _amount);
    } else {
      pushFeedbackToVault(_chainId, _vault, _relayerFee);
      xTransfer(_asset, _amount, _vault, _chainId, _slippage, _relayerFee);
    }
  }
```

xTransfer() also uses `IERC20(_token).transferFrom`:

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/XProvider.sol#L133-L147

```solidity
  function xTransfer(
    address _token,
    uint256 _amount,
    address _recipient,
    uint32 _destinationDomain,
    uint256 _slippage,
    uint256 _relayerFee
  ) internal {
    require(
      IERC20(_token).allowance(msg.sender, address(this)) >= _amount,
      "User must approve amount"
    );

    // User sends funds to this contract
    IERC20(_token).transferFrom(msg.sender, address(this), _amount);
```

## Tool used

Manual Review

## Recommendation

Consider using `safeTransfer` for all externally managed tokens, including DAI, USDC, USDT.