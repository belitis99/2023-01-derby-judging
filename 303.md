HonorLt

high

# Protocols dependence on each other

## Summary

The system is depending that each of the protocols successfully performs the requested action.

## Vulnerability Detail

DAO can add a new protocol and attach it to the vault:
```solidity
  function addProtocol(
    string calldata _name,
    uint256 _vaultNumber,
    address _provider,
    address _protocolLPToken,
    address _underlying,
    address _govToken,
    uint256 _uScale
  ) external onlyDao returns (uint256) {
    uint256 protocolNumber = latestProtocolId[_vaultNumber];

    protocolNames[_vaultNumber][protocolNumber] = _name;
    protocolGovToken[_vaultNumber][protocolNumber] = _govToken;
    protocolInfo[_vaultNumber][protocolNumber] = ProtocolInfoS(
      _protocolLPToken,
      _provider,
      _underlying,
      _uScale
    );

    emit SetProtocolNumber(protocolNumber, _protocolLPToken);

    latestProtocolId[_vaultNumber]++;

    return protocolNumber;
  }
```

The system iterates over all the protocols in many cases, for example, when withdrawing funds:
```solidity
  function pullFunds(uint256 _value) internal {
    uint256 latestID = controller.latestProtocolId(vaultNumber);
    for (uint i = 0; i < latestID; i++) {
      ...
      withdrawFromProtocol(i, amountToWithdraw);
      ...
    }
  }
```

Because it iterates from the first to the last, it means that if any of the protocols fail to execute the action, all the remaining protocols will not get a chance to cover it. There are many more functions that perform similar iterations and actions.

An admin can try to blacklist the protocol, however,  it will not be possible if `withdrawFromProtocol` fails due to external reasons:
```solidity
  function blacklistProtocol(uint256 _protocolNum) external onlyGuardian {
    uint256 balanceProtocol = balanceUnderlying(_protocolNum);
    currentAllocations[_protocolNum] = 0;
    controller.setProtocolBlacklist(vaultNumber, _protocolNum);
    savedTotalUnderlying -= balanceProtocol;
    withdrawFromProtocol(_protocolNum, balanceProtocol);
  }
```

A specific example to illustrate the problem:
1) DAO adds 3 protocols: Aave, Beta, and Compound.
2) Users deposit funds.
3) A problem is discovered in Beta contracts and they pause their contracts.
4) Users try to withdraw, there are not enough funds on Aave, but the Compound is inaccessible because the Beta provider reverts.

All the other protocols cannot function if one of them reverts.

## Impact

The system relies upon all the protocols to succeed. If one of the protocols reverts, it will halt the execution of all the dependent functions. Once added there is no way to remove, edit, reorder, or pause the protocol leaving no possibility to recover.

## Code Snippet

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Controller.sol#L146-L171

https://github.com/sherlock-audit/2023-01-derby/blob/main/derby-yield-optimiser/contracts/Vault.sol#L111-L127

## Tool used

Manual Review

## Recommendation

Add remove/edit/pause functionality for individual protocols, or perform the action with a try/catch and act accordingly.